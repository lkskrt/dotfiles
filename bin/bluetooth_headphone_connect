#!/usr/bin/env fish

set msgID 1089719283
set icon audio-headphones

set mac $argv[1]
if test -z "$mac"
    echo 'Specify headphone mac address'
    exit 1
end

# E.g. a2dp-sink-aac, headset-head-unit
set cardProfile $argv[2]
if test -z "$cardProfile"
    set cardProfile "a2dp-sink-aptx"
end

set underscoreMac (string replace -a : _ $mac)

function handle_error
    dunstify -i $icon -r $msgID -u critical "Error connecting headphones" "$action\n$message"
    exit 1
end

if not bluetoothctl info $mac | grep 'Connected: yes'
  set action "Headphones connecting ..."
  dunstify -i $icon -r $msgID -u low $action
  set message (bluetoothctl connect $mac 2>&1)
  if test $status -ne 0
      handle_error
  end

  dunstify -i $icon -r $msgID -u low "Waiting for card to become available ..."
  sleep 3
end

set action "Setting card profile to $cardProfile ..."
dunstify -i $icon -r $msgID -u low $action
set message (pactl set-card-profile bluez_card.$underscoreMac $cardProfile 2>&1)
if test $status -ne 0
    handle_error
end

set action "Setting default output ..."
dunstify -i $icon -r $msgID -u low $action
set message (pactl set-default-sink bluez_output.$underscoreMac.1 2>&1)
if test $status -ne 0
    handle_error
end

dunstify -i $icon -r $msgID "Headphones connected"
