#!/usr/bin/env fish

set msgID 1089719283
set icon audio-headphones

set mac $argv[1]
if test -z "$mac"
    echo 'Specify headphone mac address'
    exit 1
end

set underscoreMac (string replace -a : _ $mac)

function handle_error
    dunstify -i $icon -r $msgID -u critical "Error connecting headphones" $message
    exit 1
end

if not bluetoothctl info $mac | grep 'Connected: yes'
  dunstify -i $icon -r $msgID -u low "Headphones connecting ..."
  set message (bluetoothctl connect $mac 2>&1)
  if test $status -ne 0
      handle_error
  end

  dunstify -i $icon -r $msgID -u low "Waiting for card to become available ..."
  sleep 3
end

dunstify -i $icon -r $msgID -u low "Setting card profile ..."
# Try aptx first
if not pactl set-card-profile bluez_card.$underscoreMac a2dp-sink-aptx
    # Fallback to aac
    set message (pactl set-card-profile bluez_card.$underscoreMac a2dp-sink-aac 2>&1)
    if test $status -ne 0
        handle_error
    end
end

dunstify -i $icon -r $msgID -u low "Setting default output ..."
set message (pactl set-default-sink bluez_output.$underscoreMac.1 2>&1)
if test $status -ne 0
    handle_error
end

dunstify -i $icon -r $msgID "Headphones connected"
